#-------------------------------------------------------------------------------
# Laboratory scale cylindrical sample
#-------------------------------------------------------------------------------
# AUTHOR:
#   Hom Nath Gharti, Princeton University (2016)
#   Hom Nath Gharti, Queen's University (2022-)
# HISTORY:
#   March  13, 2016: First created. Tested with Trelis 15.2
#   August 05, 2022: Updated. Tested with Coreform Cubit 2022.6
#-------------------------------------------------------------------------------
reset
set node constraint on
create Cylinder height 127 radius 25.4 
create Cylinder height 60 radius 2.6 
rotate Volume 2 angle 90  about X include_merged 
subtract body 2 from body 1 
compress all
save cub5 "cylinder.cub5" overwrite journal 

pause

webcut body all with plane zplane offset 0 
webcut body all with plane yplane offset 0 
webcut body all with plane xplane offset 0 
delete Volume 2 6 7 8 3 4 5
compress all

pause

volume 1 size 1.5
surface 6 1 size 1.5
surface 6 1 scheme pave
mesh surface 6 1

pause

volume 1  redistribute nodes off 
volume 1  scheme Sweep  source surface 1 6    target surface 4   sweep transform least squares 
volume 1  autosmooth target off 
mesh volume 1 

pause

Volume 1  copy reflect x 
Volume 2 1  copy reflect y 
Volume 2 1 3 4  copy reflect z 

imprint all
merge all
compress all

pause

set duplicate block elements off
block 1 add volume all 
Sideset 1 add surface 13 17 4 8 
sideset 1 name "absorbing_surface_file_zmax"
Sideset 2 add surface 34 31 23 27 
sideset 2 name "absorbing_surface_file_zmin"
Sideset 3 add surface 14 32 9 24 5 28 18 35 
sideset 3 name "absorbing_surface_file_lateral"
save cub5 "cylinder_mesh.cub5" overwrite journal 
set large exodus file on
export mesh "cylinder_mesh.e" dimension 3 overwrite 
