#-------------------------------------------------------------------------------
# Three layered model
#-------------------------------------------------------------------------------
# AUTHOR:
#   Hom Nath Gharti, Princeton University (2016)
#   Hom Nath Gharti, Queen's University (2022-)
# HISTORY:
#   March  13, 2016: First created. Tested with Trelis 15.2
#   August 05, 2022: Updated. Tested with Coreform Cubit 2022.6
#-------------------------------------------------------------------------------
reset
set node constraint on
brick x 200 y 160 z 100
move Volume 1 x 0 y 0 z -50 include_merged 
webcut body all with plane zplane offset -70 
webcut body all with plane zplane offset -40 
save cub5 "layers_geometry.cub5" overwrite journal 

pause

imprint all
merge all

volume all size 10
volume all scheme Auto
mesh volume all

pause

refine vol 1 using plane zplane depth 1

pause

set duplicate block elements off
block 1 add volume  
set duplicate block elements off
block 1 add volume 1  
set duplicate block elements off
block 2 add volume 3  
set duplicate block elements off
block 3 add volume 2  

Sideset 1 add surface 1  
sideset 1 name "absorbing_surface_file_zmax"
Sideset 2 add surface 2  
sideset 2 name "absorbing_surface_file_zmin"
Sideset 3 add surface 12 13 6  
sideset 3 name "absorbing_surface_file_xmin"
Sideset 4 add surface 10 16 4  
sideset 4 name "absorbing_surface_file_xmax"
Sideset 5 add surface 9 15 5  
sideset 5 name "absorbing_surface_file_ymin"
Sideset 6 add surface 11 14 7  
sideset 6 name "absorbing_surface_file_ymax"
merge all
compress all
save cub5 "layers_mesh.cub5" overwrite journal 
set large exodus file on
export mesh "layers_mesh.e" dimension 3 overwrite 

pause

delete mesh volume all propagate
volume 2  size 10
volume 3  size 5
volume 1  size 2

surface 9 15 5  scheme pave
mesh surface 9 15 5 
volume 1 3 2  redistribute nodes off 
volume 1 3 2  scheme Sweep sweep transform least squares 
volume 1 3 2  autosmooth target off 
mesh volume 1 3 2 
merge all
compress all
save cub5 "layers_mesh_alternative.cub5" overwrite journal 
set large exodus file on
export mesh "layers_mesh_alternative.e" dimension 3 overwrite 
